;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;      File        :   ion_guide.gem                                             ;;;;;;;;;;
;;;;;;;;;;      Author      :   X. Chen                                                   ;;;;;;;;;;
;;;;;;;;;;      Description :   geometric definition of an ion guide                      ;;;;;;;;;;
;;;;;;;;;;      Note        :   it is made of stacked ring electrodes and housed in       ;;;;;;;;;;
;;;;;;;;;;                      a CF100 vacuum pipe                                       ;;;;;;;;;;
;;;;;;;;;;      License     :   GNU GPLv3                                                 ;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;                              Calculate parameters                              ;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

$(
local IG                =   _G.ion_guide

local ring_num          =   IG.ring_num
local ring_pitch        =   IG.ring_pitch
local ring_thickness    =   IG.ring_thickness
local ring_inner_radius =   IG.ring_inner_radius
local ring_outer_radius =   IG.ring_outer_radius

local pipe_inner_radius =   IG.pipe_inner_radius
local pipe_thickness    =   IG.pipe_thickness

local gap_left          =   IG.gap_left
local gap_right         =   IG.gap_right

local grid_size         =   IG.grid_size
)

$(
local first_ring_start  =   pipe_thickness + gap_left
local total_length      =   first_ring_start + ring_pitch*(ring_num-1) + ring_thickness + gap_right + pipe_thickness

local grid_num_axial    =   math.ceil( total_length                        / grid_size) + 1
local grid_num_radial   =   math.ceil((pipe_inner_radius + pipe_thickness) / grid_size) + 1
)


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;                             Define potential array                             ;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

pa_define($(grid_num_axial), $(grid_num_radial), 1, cylindrical, y, electrostatic,, $(grid_size), surface=fractional)

; vacuum pipe
electrode(1000) {
    fill {  within { box( 0, 0, $(pipe_thickness), $(pipe_inner_radius + pipe_thickness) ) }
            within { box( 0, $(pipe_inner_radius), $(total_length), $(pipe_inner_radius + pipe_thickness) ) }
            within { box( $(total_length - pipe_thickness), 0, $(total_length), $(pipe_inner_radius + pipe_thickness) ) }
    }
}

; ion guide
# for i = 1,ring_num do
electrode( $(i) ) { locate( $(first_ring_start + ring_pitch*(i-1)), $(ring_inner_radius) ) {
    fill {  within { box( 0, $(ring_thickness/2), $(ring_thickness), $(ring_outer_radius - ring_inner_radius) ) }
            within { circle( $(ring_thickness/2), $(ring_thickness/2), $(ring_thickness/2) ) }
    }
}}
# end
